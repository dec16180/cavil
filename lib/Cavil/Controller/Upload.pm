# Copyright (C) 2020 SUSE Linux GmbH
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, see <http://www.gnu.org/licenses/>.

package Cavil::Controller::Upload;
use Mojo::Base 'Mojolicious::Controller', -signatures;

use Digest::MD5;
use Mojo::File qw(path);
use Mojo::Util qw(md5_sum);

sub index ($self) {
  $self->render('upload/form');
}

sub store ($self) {
  my $validation = $self->validation;
  $validation->required('name')->like(qr/^[A-Za-z0-9\-\.]+$/);
  $validation->required('licenses')->like(qr/^[A-Za-z0-9\-\.]+$/);
  $validation->required('tarball')->upload->size(1, undef);
  return $self->render('upload/form') if $validation->has_error;

  my $upload = $validation->param('tarball');
  my $file   = $upload->asset->to_file;
  my $md5    = Digest::MD5->new;
  $md5->addfile($file->handle);
  my $name     = $validation->param('name');
  my $licenses = $validation->param('licenses');
  my $sum      = md5_sum($name . $licenses . $md5->hexdigest);

  my $pkgs = $self->packages;
  if (my $obj = $pkgs->find_by_name_and_md5($name, $sum)) {
    $self->flash(message => "Package $name with checksum $sum already exists");
    return $self->render('dashboard');
  }

  my $user = $self->users->find(login => $self->current_user);
  my $id   = $pkgs->add(
    name            => $name,
    checkout_dir    => $sum,
    api_url         => '',
    requesting_user => $user->{id},
    project         => '',
    priority        => 5,
    package         => $name,
    created         => undef,
    srcmd5          => $sum,
  );
  my $dir = path($self->app->config->{checkout_dir}, $name, $sum)->make_path;
  $dir->child("$name.spec")->spurt(<<"EOF");
#
# spec file for package $name
#
# Copyright (c) 2020 SUSE LLC
#
# All modifications and additions to the file contributed by third parties
# remain the property of their copyright owners, unless otherwise agreed
# upon. The license for this file, and modifications and additions to the
# file, is the same license as for the pristine package itself (unless the
# license for the pristine package is not an Open Source License, in which
# case the license is the MIT License). An "Open Source License" is a
# license that conforms to the Open Source Definition (Version 1.9)
# published by the Open Source Initiative.

#
# This file is a placeholder generated by Cavil to store package metadata
# for manual uploads and not a real spec file
#

Name:    $name
License: $licenses
EOF
  my $filename = path($upload->filename)->basename;
  $file->move_to($dir->child($filename));
  my $obj = $pkgs->find($id);
  $obj->{external_link} = 'upload';
  $pkgs->update($obj);
  $pkgs->imported($id);

  $pkgs->unpack($id);
  $self->flash(message => "Package $name has been uploaded and is now being processed");
  $self->redirect_to('dashboard');
}

1;
